apply plugin: 'java'

analyzeTestClassesDependencies.enabled = false

dependencies {
    testCompile "com.google.guava:guava:${project.property('guava.version')}"
    testCompile "com.spotify:docker-client:${project.property('docker-client.version')}"
    testCompile "junit:junit:${project.property('junit.version')}"
    testCompile "org.assertj:assertj-core:${project.property('assertj.version')}"
    testCompile "com.jayway.restassured:rest-assured:${project.property('restassured.version')}"
    testCompile "com.jayway.restassured:json-path:${project.property('restassured.version')}"
}

sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    componentTests {
        java {
            srcDir 'src/component-test/java'
        }
        resources {
            srcDir 'src/component-test/resources'
        }
        //compileClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    componentTestsCompile.extendsFrom testCompile
    componentTestsRuntime.extendsFrom testRuntime
}

task componentTest(type: Test) {
    description = "Runs component tests (located in src/component-test/...)."
    testClassesDir = project.sourceSets.componentTests.output.classesDir
    classpath = project.sourceSets.componentTests.runtimeClasspath
}